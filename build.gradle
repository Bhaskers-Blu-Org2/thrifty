allprojects {
    repositories {
        mavenCentral()
    }

    group GROUP
    version VERSION

    project.ext {
        libraries = [
                okio: [
                        'com.squareup.okio:okio:1.6.0'
                ],

                javaPoet: [
                        'com.squareup:javapoet:1.4.0'
                ],

                testing: [
                        'junit:junit:4.12',
                        'org.mockito:mockito-core:1.10.19'
                ]
        ]
    }
}

buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
}

subprojects { sp ->
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'checkstyle'

    sourceCompatibility = '1.7'
    targetCompatibility = '1.7'

    checkstyle {
        configFile = rootProject.file("config/checkstyle/checkstyle.xml")
        configProperties = ["checkstyle.cache.file": file('build/checkstyle.cache')]
        showViolations = true
        sourceSets = [sourceSets.main]
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        from 'build/docs/javadoc'
        classifier = 'javadoc'
    }

    task sourcesJar(type: Jar) {
        from sourceSets.main.allSource
        classifier = 'sources'
    }

    artifacts {
        archives jar
        archives javadocJar
        archives sourcesJar
    }
}

afterEvaluate {
    subprojects {
        apply from: file("${rootDir}/gradle/gradle-mvn-push.gradle")
    }
}
